<program> ::= <preprocessor> <statements> "EOF"

<preprocessor> ::= ("import"<token> <endl>)*
<statements> ::= (<statement>)+
<statement> ::= (<declaration>  | <expression> | <if> | <while> | <for> | <return> | <break> | <continue> | <function> | <class_s> | <print>) <endl>

<declaration> ::= <declaration_token>
<type>::="int"|"float"|"string"|"char"|"bool"
<declaration_token>::=<type> <set_token>("," <set_token>)*
<set_token> ::= <token> | <token> "=" <expression> | <generic_token> | <generic_token> <body_start> <initial_list> <body_end>
<token>::=<char>(<char>|<number>)*
<generic_token>::=<token>"<"(<token>|<generic_token>)*">"
<initial_list> ::= <expression> | <expression> "," <initial_list>

<if> ::= "if" "(" <expression> ")" <body_start> <statements> <body_end> (<elif>)* ("else" <body_start> <statements> <body_end>)?
<elif> ::= "elif" "(" <expression> ")" <body_start> <statements> <body_end>
<while> ::= "while" "(" <expression> ")" <body_start> <statements> <body_end>
<for> ::= "for" "(" <declaration> ";" <expression> ";" <expression> ")" <body_start> <statements> <body_end>
<return> ::= "return" <expression>
<break> ::= "break"
<continue> ::= "continue"

<function> ::= "func" <token> "(" <arg_list> ")" "->" <type> <body_start> <statements> <body_end>
<arg_list>::= <type> <token>(","<type> <token>)* | EMPTY

<class_s> ::= "class" <token> <body_start> <class_statements> <body_end>
<class_statements> ::= <class_statement> (";" <class_statement>)*
<class_statement> ::= (<access_type> <declaration_token> | <access_type> <function> | <class_property>) <endl>
<access_type> ::= "public"|"private"
<class_property> ::= "property" <type> <token>

<print> ::= "print" "(" <expression> ")"


<expression> ::= <assigment_expression> (","<assigment_expression>)*
<assigment_expression> ::= <logical_or> <assign_operator> <init_expression> | <logical_or>
<init_expression> ::= <assigment_expression> | <init_list>
<init_list> ::= <body_start> <init_list_seq> <body_end>
<init_list_seq> ::= <expression> | <expression>","<init_list_seq>

<logical_or> ::= <logical_and> "||" <logical_or> | <logical_and>
<logical_and> ::= <bit_or> "&&" <logical_and> | <bit_or>

<bit_or> ::= <bit_xor> "|" <bit_or> | <bit_xor>
<bit_xor> ::= <bit_and> "^" <bit_xor> | <bit_and>
<bit_and> ::= <equal_comparison> "&" <bit_and> | <equal_comparison>

<equal_comparison> ::= <compare> ("==" | "!=") <equal_comparison> | <compare>
<compare> ::= <shift> (">" | ">=" | "<" | "<=") <compare> | <shift>

<shift> ::= <sum_sub> (">>" | "<<") <shift> | <sum_sub>

<sum_sub> ::= <mul_div> <sign> <sum_sub> | <mul_div>

<mul_div> ::= <power> ("/" | "*" | "%") <mul_div> | <power>

<power> ::= <unary> "**" <power> | <unary>

<unary> ::= ("+" | "-" | "~" | "!" | "++" | "--") <unary> | <postfix>

<postfix> ::= <token_exp> ("++" | "--") | <token_exp>

<token_exp> ::= <token> "(" <expression> ")" | <literal> "(" <expression> ")" | "(" <expression> ")"

<char>::="a-z"|"A-Z"
<digit>::="0-9"
<sign>::="+"|"-"
<endl>::=";"
<body_start>::="{"
<body_end>::="}"

<literal>::=<number>|<string>|<boolean>
<string>::="\""<char>*"\""
<number>::=<digit>+"."<digit>*|<digit>+"."|<digit>+
<boolean>::="true"|"false"

<binary_operator>::="*"|"+"|"/"|"**"|">>"|"<<"|">"|">="|"<"|"<="
<assign_operator>::=[<binary_operator>]"="
